buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath group: 'io.freefair.gradle', name: 'lombok-plugin', version: '6.6.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'io.freefair.lombok'

file('gradle.properties').withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "$mod_version"
group = "hantonik.$mod_id"
archivesBaseName = "$mod_name-$mc_version"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}

println "\nStarting build of $mod_name, Version: $mod_version"
println "Using Forge: $forge_version, for Minecraft: $mc_version, with Mappings: $mappings_version-$mappings_channel\n"

minecraft {
    mappings channel: "$mappings_channel", version: "$mappings_version"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', "$mod_id"

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "$projectDir/build/createSrgToMcp/output.srg"

            ideaModule "${project.name}.main"

            mods {
                anvilapi {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', "$mod_id"

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "$projectDir/build/createSrgToMcp/output.srg"

            ideaModule "${project.name}.main"

            mods {
                anvilapi {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url 'https://maven.blamejared.com' }

    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"

    compileOnly fg.deobf("mezz.jei:jei-$mc_version-common-api:$jei_version")
    compileOnly fg.deobf("mezz.jei:jei-$mc_version-forge-api:$jei_version")
    implementation fg.deobf("mezz.jei:jei-$mc_version-forge:$jei_version")

    compileOnly "org.projectlombok:lombok:$lombok_version"

    annotationProcessor "org.spongepowered:mixin:$mixin_processor_version:processor"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"

}

task replaceResources(type: Copy) {
    outputs.upToDateWhen { false }

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand  'mod_version': "$version",
                'mod_name': "$mod_name"
    }

    into "$buildDir/resources/main/"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }

    finalizedBy replaceResources
}

jar {
    manifest {
        attributes([
                "Specification-Title": archivesBaseName,
                "Specification-Vendor": "Hantonik",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Implementation-Vendor": "Hantonik",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy('reobfJar')
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar

            artifactId "$archivesBaseName"
        }
    }
    repositories {
        maven {
            url "https://hantonikdev.jfrog.io/artifactory/mods/"

            credentials {
                username System.getenv("REPO_USER")
                password System.getenv("REPO_PASSWORD")
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
