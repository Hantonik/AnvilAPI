apply from: 'buildtools/ColoredOutput.gradle'

file('gradle.properties').withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

println "Starting build of $mod_name, Version: $mod_version"

subprojects {
    version = mod_version
    group = "hantonik.$mod_id"

    def replaceProperties = [
        'mod_version': version,
		'mod_name': mod_name,

		'mc_version': mc_version,

		'loader_version': loader_version,
		'fabric_version': fabric_version
    ]

    tasks.withType(ProcessResources).configureEach {
        inputs.properties replaceProperties
	    replaceProperties.put 'project', project

	    filesMatching(List.of("META-INF/mods.toml", "fabric.mod.json")) {
	    	expand replaceProperties
	    }
    }

    tasks.withType(Jar).configureEach {
        manifest {
            attributes([
                    "Specification-Title": mod_id,
                    "Specification-Vendor": "Hantonik",
                    "Specification-Version": "1",
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.jar.archiveVersion,
                    "Implementation-Vendor": "Hantonik",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
}